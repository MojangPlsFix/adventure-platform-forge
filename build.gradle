buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath 'org.spongepowered:mixingradle:0.7+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = '0.1.0'
group = 'net.kyori.adventure.platform.forge'
archivesBaseName = 'adventure-platform-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.18.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run_client')
            arg '-mixin.config=mixins.adventure_platform_forge.json'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
            }
        }

        server {
            workingDirectory project.file('run_server')
            arg '-mixin.config=mixins.adventure_platform_forge.json'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                adventure_platform_forge {
                    source sourceSets.main
                }
                /*
                adventure_platform_forge_test {
                    source sourceSets.test
                }
                */
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    shade
    implementation.extendsFrom shade
}

repositories {
    mavenCentral()
    maven {
        name = "sonatype-oss-snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.2.0'

    // Apply Mixin AP
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    shade "net.kyori:adventure-api:4.11.0"
    shade "net.kyori:adventure-key:4.11.0"

    shade "net.kyori:adventure-platform-api:4.1.0"

    shade "net.kyori:adventure-text-minimessage:4.11.0"
    shade "net.kyori:adventure-text-serializer-gson:4.11.0"
    shade "net.kyori:adventure-text-serializer-plain:4.11.0"

    shade "net.kyori:examination-api:1.3.0"
    shade "net.kyori:examination-string:1.3.0"

    shade "org.jetbrains:annotations:23.1.0"
}

mixin {
    add sourceSets.main, 'mixins.adventure_platform_forge.refmap.json'
    config 'mixins.adventure_platform_forge.json'

    debug.verbose = true
    debug.export = true
}

shadowJar {
    mergeServiceFiles()
    classifier = ''
    configurations = [project.configurations.shade]

    relocate('net.kyori', "${project.group}.shadow.net.kyori") {
        exclude 'net/kyori/adventure/platform/forge/**'
    }

    relocate 'org.jetbrains', "${project.group}.shadow.org.jetbrains"
    relocate 'org.intellij', "${project.group}.shadow.org.intellij"


    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude 'META-INF/versions/**'
    exclude 'org/slf4j/**'
    exclude 'com/google/**'
}

tasks.shadowJar.dependsOn('reobfJar')

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.outputs.files.singleFile
    }
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.outputs.files.singleFile
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "adventure_platform_forge",
                "Specification-Vendor"    : "KyoriPowered",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "KyoriPowered",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'            : 'mixins.adventure_platform_forge.json'
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.compilerArgs += ['-Xlint:-processing']
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/MojangPlsFix/adventure-platform-forge"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId = "net.kyori.adventure"
            artifactId = "adventure-platform-forge"

            artifact tasks.shadowJar

            pom {
                name = 'adventure-platform-forge'
                description = 'Integration between adventure and Forge platform.'
                url = 'https://github.com/MyFTB/adventure-platform-forge'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/MyFTB/adventure-platform-forge/blob/mc/1.18/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'MojangPlsFix'
                        name = 'MojangPlsFix'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/MyFTB/adventure-platform-forge'
                }
            }
        }
    }
}
